use crate::ast::*;
use std::str::FromStr;
use std::rc::Rc;

grammar;

//# Ignore patterns.
//# - Ignore most whitespace (spaces, tabs, and most newlines)
//# - Wse newlines to define the end of a comment.
//# - Only ignore single hash comments # comment
//# - Double hash comments are *not* ignored, they are stored.
//# - Due to limitation of Lalrpop (until I write a custom lexer),
//#     doc comments are only allowed in statement position.
//# - Doc comments are stored in the metadata of the function they are inside.

match {
    r" +" => {},
    r"\t+" => {},
    r"\#\#[^\n]*\n" => DOC_COMMENT, //# capture everything between the ## and the \n as a doc comment
} else {
    r"\#(?:[^\#][^\n]*)?\n" => {}, //# ignore normal comments
    r"\n" => {},
    _
}

DocComment: String = {
    <comment:DOC_COMMENT> => comment.to_owned(),
}


//# Macro for making comma-separated lists of anything
Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        },
    },
};

CommaBefore<T>: Vec<T> = {
    <v:("," <T>)*> => v,
}

Block: Rc<Block> = {
	"{" <b:BlockInnerNotEmpty> "}" => b,
};

//# Kal files are "block inners". Essentially they are
//#   zero parameter functions.
//# We explicitly allow empty files. Normal blocks
//#   can't be empty because that would be ambiguous
//#   with empty objects.
pub BlockInner: Rc<Block> = {
    <comments:(<DocComment>)*> =>
        Rc::new(Block {
            statements: Vec::new(),
            expression: None,
            comments,
        }),
    <b:BlockInnerNotEmpty> => b,
}

//# Blocks don't allow the block to be empty. This
//#   means that lone curly braces are empty objects
//#   instead.
//# However, an empty ast::Block *can* exist via
//#   an empty file
pub BlockInnerNotEmpty: Rc<Block> = {
    <comments:(<DocComment>)*> <e:Expression> <comments2:(<DocComment>)*> => {
        let (mut comments, mut comments2) = (comments, comments2);
        comments.append(&mut comments2);
        Rc::new(Block {
            statements: Vec::new(),
            expression: Some(e),
            comments,
        })
    },
    <comments:(<DocComment>)*> <v:(<Statement> ";" <(DocComment)*>)+> <e:(<Expression> <(DocComment)*>)?> => {
        let mut comments = comments;
        let mut v = v;
        let statements = v.into_iter().map(|(stat, mut comm)| {
            comments.append(&mut comm);
            stat
        }).collect::<Vec<_>>();
        let expression = e.map(|(expr, mut comm)| {
            comments.append(&mut comm);
            expr
        });
        Rc::new(Block {
            statements,
            expression,
            comments,
        })
    },
}

Statement: Rc<dyn Statement> = {
    <le:LetStatement> => le,
    <nf:NamedFunction> => nf,
    <a:Assignment> => a,
    <e:Expression> => e.into_statement(),
}

//# Function literal is highest operator
//#   precedence. Everything after fn () is
//#   considered part of the function literal.
//#   Break, continue and send also capture all
//#   of the following expression.
Expression: Rc<dyn Expression> = {
    <function:Function> => function,
    <send:SendExpr> => send,
    <continue_expr:Continue> => continue_expr,
    <break_expr:Break> => break_expr,
    BooleanExpression,
}

BooleanExpression: Rc<dyn Expression> = {
    <left:BooleanExpression> <operator:BooleanOperator> <right:ComparisonExpression> =>
        Rc::new(BooleanExpression {
            left,
            right,
            operator,
        }),
    NotExpression,
}

BooleanOperator: BooleanOperator = {
    "and" => BooleanOperator::And,
    "or" => BooleanOperator::Or,
    "xor" => BooleanOperator::Xor,
}

NotExpression: Rc<dyn Expression> = {
    "not" <expr:NotExpression> =>
        Rc::new(NotExpression {
            expr,
        }),
    ComparisonExpression,
}

ComparisonExpression: Rc<dyn Expression> = {
    <left:ComparisonExpression> <operator:ComparisonOperator> <right:AddExpression> =>
        Rc::new(ComparisonExpression {
            left,
            right,
            operator,
        }),
    AddExpression,
}

ComparisonOperator: ComparisonOperator = {
    "==" => ComparisonOperator::Equal,
    "!=" => ComparisonOperator::NotEqual,
    "<=" => ComparisonOperator::LessEqual,
    "<" => ComparisonOperator::Less,
    ">=" => ComparisonOperator::GreaterEqual,
    ">" => ComparisonOperator::Greater,
}

AddExpression: Rc<dyn Expression> = {
    <left:AddExpression> "+" <right:MultiplyExpression> =>
        Rc::new(NumericExpression {
            left,
            right,
            operator: NumericOperator::Add,
        }),
    <left:AddExpression> "-" <right:MultiplyExpression> =>
        Rc::new(NumericExpression {
            left,
            right,
            operator: NumericOperator::Subtract,
        }),
    MultiplyExpression,
}

MultiplyExpression: Rc<dyn Expression> = {
    <left:MultiplyExpression> "*" <right:NormalExpression> =>
        Rc::new(NumericExpression {
            left,
            right,
            operator: NumericOperator::Multiply,
        }),
    <left:MultiplyExpression> "/" <right:NormalExpression> =>
        Rc::new(NumericExpression {
            left,
            right,
            operator: NumericOperator::Divide,
        }),
    NegativeExpression,
}

NegativeExpression: Rc<dyn Expression> = {
    "-" <expr:NormalExpression> =>
        Rc::new(NegativeExpression {
            expr,
        }),
    NormalExpression,
}

NormalExpression: Rc<dyn Expression> = {
    <literal:Literal> => literal,
    <if_expr:IfExpression> => if_expr,
    <handle:Handle> => handle,
    <loop_expr:LoopExpression> => loop_expr,
    SimpleExpression,
}

SimpleExpression: Rc<dyn Expression> = {
    //# <continue_expr:ContinueLonely> => continue_expr,
    //# <send_expr:SendExprLonely> => send_expr,
    //# <break_expr:BreakLonely> => break_expr,
    <location:IdentLocation> => Rc::new(location),
    <location:DotLocation> => Rc::new(location),
    <location:IndexLocation> => Rc::new(location),
    <location:DotLocationExpression> => Rc::new(location),
    <location:IndexLocationExpression> => Rc::new(location),
    <func_invo:FunctionInvocation> => func_invo,
    ParenExpression,
}

LocationBaseExpression: Rc<dyn Expression> = {
    <func_invo:FunctionInvocation> => func_invo,
    ParenExpression,
}

ParenExpression: Rc<dyn Expression> = {
    "(" <e:Expression> ")" => e,
}

Function: Rc<Function> = {
    "fn" "(" <pattern:ListPattern> ")" <body:FunctionBody> =>
    Rc::new(Function {
        pattern: Rc::new(pattern),
        body,
    }),
}

NamedFunction: Rc<NamedFunction> = {
    "fn" <name:Ident> "(" <pattern:ListPattern> ")" <body:FunctionBody> =>
    Rc::new(NamedFunction {
        name,
        function: Rc::new(Function {
            pattern: Rc::new(pattern),
            body,
        }),
    }),
}

SpreadPattern: SpreadPattern = {
    "..." <i:Ident?> => match i {
        None => SpreadPattern::Unnamed,
        Some(s) => SpreadPattern::Named(s),
    }
}

ListSubPattern: ListSubPattern = {
    <i:Ident> => ListSubPattern::Ident(i),
    "{" <o:ObjectPattern> "}" => ListSubPattern::Object(Box::new(o)),
    "[" <l:ListPattern> "]" => ListSubPattern::List(Box::new(l)),
}

//# ()
//# (a)
//# (a, )
//# (a, ...)
//# (...)
//# (..., )
//# (a, ..., b)
//# (a, ..., b, )
//# (..., b)
//# (..., b, )

ListPattern: ListPattern = {

    => ListPattern {
        before_patterns: vec![],
        spread_and_after_patterns: None,
    },
    <spread:SpreadPattern> <after_patterns:("," <ListSubPattern>)*> ","? => ListPattern {
        before_patterns: vec![],
        spread_and_after_patterns: Some((spread, after_patterns)),
    },
    <first_pattern:ListSubPattern> <before_patterns:("," <ListSubPattern>)*> <spread_and_after_patterns:("," <SpreadPattern> <("," <ListSubPattern>)*>)?> ","? => {
        let mut before_patterns = before_patterns;
        before_patterns.insert(0, first_pattern);
        ListPattern {
            before_patterns,
            spread_and_after_patterns,
        }
    }
}

ObjectSubPattern: ObjectSubPattern = {
    <i:Ident> => ObjectSubPattern::Ident(i),
    <i:Ident> "." "{" <o:ObjectPattern> "}" => ObjectSubPattern::Object(i, Box::new(o)),
    <i:Ident> "." "[" <l:ListPattern> "]" => ObjectSubPattern::List(i, Box::new(l)),
    <i:Ident> "." "*" => ObjectSubPattern::Object(i, Box::new(ObjectPattern {
        patterns: Vec::new(),
        final_pattern: Some(ObjectFinalPattern::Wildcard),
    })),
}

ObjectFinalPattern: ObjectFinalPattern = {
    "..." => ObjectFinalPattern::SpreadNameless,
    "..." <i:Ident> => ObjectFinalPattern::Spread(i),
    "*" => ObjectFinalPattern::Wildcard,
}

ObjectPattern: ObjectPattern = {
    => ObjectPattern {
        patterns: Vec::new(),
        final_pattern: None,
    },
    <final_pattern:ObjectFinalPattern> ","? => ObjectPattern {
        patterns: Vec::new(),
        final_pattern: Some(final_pattern),
    },
    <first_pattern:ObjectSubPattern> <patterns:("," <ObjectSubPattern>)*> <final_pattern:("," <ObjectFinalPattern>)?> ","? => {
        let mut patterns = patterns;
        patterns.insert(0, first_pattern);
        ObjectPattern {
            patterns,
            final_pattern,
        }
    },
}

FunctionBody: Rc<Block> = {
    <e: Expression> =>
        Rc::new(Block {
            statements: Vec::new(),
            expression: Some(e),
            comments: Vec::new(),
        }),
    <b: Block> => b,
}

LetPattern: Rc<LetPattern> = {
    <pattern:Ident> => Rc::new(LetPattern::Ident(pattern)),
    "[" <pattern:ListPattern> "]" => Rc::new(LetPattern::List(pattern)),
    "{" <pattern:ObjectPattern> "}" => Rc::new(LetPattern::Object(pattern)),
    "*" => Rc::new(LetPattern::Object(ObjectPattern {
        patterns: Vec::new(),
        final_pattern: Some(ObjectFinalPattern::Wildcard),
    })),
}

LetStatement: Rc<LetStatement> = {
    "let" <pattern:LetPattern> "=" <expr:Expression> =>
    Rc::new(LetStatement {
        pattern,
        expr,
    }),
}

Assignment: Rc<Assignment> = {
    <location:LocationChain> "=" <expr:Expression> =>
        Rc::new(Assignment {
            location,
            expr,
        }),
}

IfExpression: Rc<IfExpression> = {
    "if" <cond:Expression> <body:Block> <else_ifs:ElseIf*> <else_body:ElseBody?> =>
    {
        let mut ifs = else_ifs;
        ifs.insert(0, IfPart {
            cond,
            body,
        });
        Rc::new(IfExpression {
            ifs,
            else_body,
        })
    },
}

ElseIf: IfPart = {
    "else" "if" <cond:Expression> <body:Block> =>
        IfPart {
            cond,
            body,
        },
}

ElseBody: Rc<Block> = {
    "else" <b:Block> => b,
}

LoopExpression: Rc<LoopExpression> = {
    "loop" <body:Block> => Rc::new(LoopExpression {
        body,
    }),
}

FunctionInvocation: Rc<FunctionInvocation> = {
    <base:SimpleExpression> "(" <elems:Comma<ListElem>> ")" =>
        Rc::new(FunctionInvocation {
            base,
            elems,
        }),
}

Literal: Rc<dyn Expression> = {
    "null" => Rc::new(Null),
    "true" => Rc::new(Bool(true)),
    "false" => Rc::new(Bool(false)),
    <n:Int> => Rc::new(Int(n)),
    //#<s:Str> => Literal::String(s),
    <l:List> => l,
    <o:Object> => o,
}

List: Rc<List> = {
    "[" <elems:Comma<ListElem>> "]" =>
        Rc::new(List {
            elems,
        }),
}

ListElem: ListElem = {
    <e:Expression> => ListElem::Elem(e),
    "..." <e:SimpleExpression> => ListElem::Spread(e),
}

Object: Rc<Object> = {
    "{" <elems:Comma<ObjectElem>> "}" =>
        Rc::new(Object {
            elems,
        }),
}

ObjectElem: ObjectElem = {
    <i:Ident> ":" <e:Expression> => ObjectElem::Kv(i, e),
    "..." <e:SimpleExpression> => ObjectElem::Spread(e),
}

Ident: String = {
    <s:r"[_a-zA-Z][_a-zA-Z0-9]*"> => s.to_owned(),
}

Int: i64 = {
    //# `unwrap` todo convert to fallible operation (e.g. Int or TooLong)
    <i:r"0|(?:[1-9][_0-9]*)"> => {
        let string = i.to_string();
        let string = i.replace("_", "");
        i64::from_str(&string).unwrap()
    }
}


Handle: Rc<Handle> = {
    "handle" <expr:FunctionInvocation> "{" <match_arms:Comma<HandleMatch>> "}" =>
        Rc::new(Handle {
            expr,
            match_arms,
        }),
}

HandleMatch: HandleMatch = {
    <symbol:Ident> <param:Ident> <block:Block> =>
        HandleMatch {
            symbol,
            param,
            block,
        },
}

SendExpr: Rc<SendExpr> = {
    //# "with" is not *just* aesthetic here (although I don't mind
    //#   it) - it solves a grammar ambiguity. It might be solvable 
    //#   without "with". It would require having two expressions
    //#   next to each other not be ambiguous. I didn't try very
    //#   hard...
    "send" <symbol:Ident> <expr:("with" <Expression>)?> =>
        Rc::new(SendExpr {
            symbol,
            expr,
        }),
}

Continue: Rc<Continue> = {
    "continue" <expr:("with" <Expression>)?> =>
        Rc::new(Continue {
            expr,
        }),
}

Break: Rc<Break> = {
    "break" <expr:("with" <Expression>)?> =>
        Rc::new(Break {
            expr,
        }),
}

LocationChain: LocationChain = {
    <location:IdentLocation> => location,
    <location:DotLocation> => location,
    <location:IndexLocation> => location,
}

IdentLocation: LocationChain = {
    <string:Ident> => LocationChain {
        base: LocationChainBase::Ident(string),
        parts: Vec::new(),
    },
}

DotLocation: LocationChain = {
    <base:LocationChain> "." <prop:Ident> => {
        let mut base = base;
        base.parts.push(Rc::new(DotLocation {
            prop,
        }));
        base
    },
}

IndexLocation: LocationChain = {
    <base:LocationChain> "[" <index:Expression> "]" =>{
        let mut base = base;
        base.parts.push(Rc::new(IndexLocation {
            index,
        }));
        base
    },
}

LocationChainExpression: LocationChain = {
    <expr:LocationBaseExpression> => LocationChain {
        base: LocationChainBase::Expression(expr),
        parts: Vec::new(),
    },
    <location:DotLocationExpression> => location,
    <location:IndexLocationExpression> => location,
}

DotLocationExpression: LocationChain = {
    <base:LocationChainExpression> "." <prop:Ident> => {
        let mut base = base;
        base.parts.push(Rc::new(DotLocation {
            prop,
        }));
        base
    },
}

IndexLocationExpression: LocationChain = {
    <base:LocationChainExpression> "[" <index:Expression> "]" =>{
        let mut base = base;
        base.parts.push(Rc::new(IndexLocation {
            index,
        }));
        base
    },
}
